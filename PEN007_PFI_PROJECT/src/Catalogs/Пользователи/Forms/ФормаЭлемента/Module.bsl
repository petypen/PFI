&НаСервере
Процедура PEN007_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	// Новые реквизиты формы
	НовыеРеквизиты = Новый Структура;
	НовыеРеквизиты.Вставить("РеквизитДата", ПФИ.ОписаниеТипаДатаВремя("Дата"));
	НовыеРеквизиты.Вставить("РеквизитЦвет", Новый ОписаниеТипов("Цвет"));
	ПФИ.СоздатьРеквизиты(ЭтотОбъект, НовыеРеквизиты);
	
	// Новая страница "Группа ПФИ" в группе страниц "Страницы"
	ГруппаСтраниц = ЭтотОбъект.Элементы.Страницы;
	ГруппаПФИ = ПФИ.СтраницаГруппыСтраниц(ЭтотОбъект, "ГруппаПФИ", ГруппаСтраниц, "Группа ПФИ", 1);
	
	// Новая декорация "Декорация Заголовок" в "Группе ПФИ". Дополнительно установлены Свойства ЦветТекста и ЦветФона
	ДекорацияЗаголовок = ПФИ.ДекорацияНадпись(ЭтотОбъект, "ДекорацияЗаголовок", ГруппаПФИ,
		"Пример использования библиотеки ПФИ");
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("ЦветТекста", WebЦвета.Белый);
	СвойстваЭлемента.Вставить("ЦветФона", WebЦвета.СинеСерый);
	ПФИ.УстановитьСвойстваЭлементу(ДекорацияЗаголовок, СвойстваЭлемента);
	
	// Новая группа "ГруппаПоля" для поля ввода
	ГруппаПоля = ПФИ.ГруппаОбычнаяБезОтображения(ЭтотОбъект, "ГруппаПоля", ГруппаПФИ, 2);
	
	// Новые поля "ПолеДата" и "ПолеЦвет" в группе "ГруппаПоля"
	ПФИ.Поле(ЭтотОбъект, "ПолеДата", ГруппаПоля, "Поле дата", 1, "РеквизитДата");
	ПолеЦвет = ПФИ.Поле(ЭтотОбъект, "ПолеЦвет", ГруппаПоля, "Поле цвет", 1, "РеквизитЦвет");
	СобытияЭлемента = Новый Структура;
	СобытияЭлемента.Вставить("ПриИзменении", "ПолеЦветПриИзменении");
	ПФИ.УстановитьСобытияЭлементу(ПолеЦвет, СобытияЭлемента);
	// Альтернативный вариант, без использования ПФИ
	//ПолеЦвет.УстановитьДействие("ПриИзменении", "ПолеЦветПриИзменении");
	
	// Новая группа "ГруппаТаблицы" для таблицы формы
	ГруппаТаблицы = ПФИ.ГруппаОбычная(ЭтотОбъект, "ГруппаТаблицы", ГруппаПФИ, "Группа таблицы");
	
	// Новая декорация "ДекорацияТаблицы" в группе "ГруппаТаблицы"
	ИмяКартинки = "Предупреждение32";
	ДекорацияКартинка = ПФИ.ДекорацияКартинка(ЭтотОбъект, "ДекорацияТаблицы", ГруппаТаблицы, ИмяКартинки);
	
	// Новые реквизиты для таблицы формы
	ПФИ.СоздатьРеквизит(ЭтотОбъект, "РеквизитТаблицаЗначений", ПФИ.ОписаниеТипаТаблицаЗначений());
	РеквизитыТаблицы = Новый Структура;
	РеквизитыТаблицы.Вставить("Наименование", ПФИ.ОписаниеТипаСтрока(20));
	РеквизитыТаблицы.Вставить("Количество", ПФИ.ОписаниеТипаЧисло(10, 2));
	ПФИ.СоздатьРеквизитыТаблицы(ЭтотОбъект, "РеквизитТаблицаЗначений", РеквизитыТаблицы);

	КолонкиТаблицы = Новый Структура;
	КолонкиТаблицы.Вставить("Наименование", "Наименование");
	КолонкиТаблицы.Вставить("Количество", "Количество");

	ПФИ.ТаблицаФормы(ЭтотОбъект, "ТаблицаДвеКолонки", ГруппаТаблицы, "РеквизитТаблицаЗначений", КолонкиТаблицы);
	
	// Новая кнопка "ЗаполнитьТаблицу"
	ПФИ.СоздатьКоманду(ЭтотОбъект, "КомандаЗаполнитьТаблицу", "Заполнить таблицу", "ЗаполнитьТаблицу");
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Картинка", БиблиотекаКартинок.ДобавитьЭлементСписка);
	СвойстваЭлемента.Вставить("Отображение", ОтображениеКнопки.КартинкаИТекст);
	ПФИ.Кнопка(ЭтаФорма, "ЗаполнитьТаблицу", ГруппаТаблицы, , "КомандаЗаполнитьТаблицу", 1, СвойстваЭлемента,
		ДекорацияКартинка);
		
	// Новая группа "ГруппаДинамическогоСписка"
	ГруппаДинамическогоСписка = ПФИ.ГруппаОбычная(ЭтотОбъект, "ГруппаДинамическогоСписка", ГруппаПФИ,
		"Группа динамического списка");

	ТекстЗапроса = ТекстЗапросаДинамическогоСписка();
	ПараметрыЗапроса = Новый Структура("ПометкаУдаления", Ложь);
	КолонкиТаблицы = Новый Структура();
	КолонкиТаблицы.Вставить("Наименование", "Пользователь");
	КолонкиТаблицы.Вставить("Подразделение", "Подразделение");
	КолонкиТаблицы.Вставить("ФизическоеЛицо", "ФИО");
	
	ПФИ.ДинамическийСписок(ЭтотОбъект, "ДинамическийСписок", ГруппаДинамическогоСписка,
		ТекстЗапроса, ПараметрыЗапроса, , КолонкиТаблицы);
		
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаДинамическогоСписка()

	ТекстЗапроса = "ВЫБРАТЬ
	|	Пользователи.Наименование КАК Наименование,
	|	Пользователи.Подразделение КАК Подразделение,
	|	Пользователи.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ПометкаУдаления = &ПометкаУдаления";

	Возврат ТекстЗапроса;
КонецФункции

// @skip-check module-unused-method
&НаКлиенте
Процедура ПолеЦветПриИзменении(Элемент)
	ЭтотОбъект.Элементы.ДекорацияЗаголовок.ЦветФона = ЭтотОбъект.РеквизитЦвет;
КонецПроцедуры

// @skip-check module-unused-method
&НаКлиенте
Процедура ЗаполнитьТаблицу(Команда)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры

// @skip-check module-unused-method
&НаСервере
Процедура ЗаполнитьТаблицуНаСервере()
	ДанныеФормыКоллекция = ЭтотОбъект.РеквизитТаблицаЗначений; // ДанныеФормыКоллекция -

	ДанныеФормыКоллекция.Очистить();
	Для индекс = 1 По 5 Цикл
		ЭлементКоллекции = ДанныеФормыКоллекция.Добавить();
		ЭлементКоллекции.Наименование = "Строка " + Строка(индекс);
		ЭлементКоллекции.Количество = индекс;
	КонецЦикла;
КонецПроцедуры